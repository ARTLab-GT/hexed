{utilities}
version = version_major + {.} + version_minor + {.} + version_patch
license = {This software (Hexed) is currently not available under any license. Posession or usage outside of the Georgia Tech Research Corporation (including ARTLab) is not authorized.}
heat_rat = 1.4
momentum_expr = {momentum0 = momentum0; momentum1 = momentum1; momentum2 = momentum2;}
density_expr = {density = density;}
energy_expr = {energy = energy;}
residual_momentum_expr = {residual_momentum0 = (momentum0 - prev_momentum0)/tss; residual_momentum1 = (momentum1 - prev_momentum1)/tss; residual_momentum2 = (momentum2 - prev_momentum2)/tss;}
residual_density_expr = {residual_density = (density - prev_density)/tss;}
residual_energy_expr = {residual_energy = (energy - prev_energy)/tss;}
residual_expr = residual_momentum_expr + residual_density_expr + residual_energy_expr
velocity_expr = {velocity0 = momentum0/density; velocity1 = momentum1/density; velocity2 = momentum2/density;}
speed_expr = {speed = sqrt(velocity0^2 + velocity1^2 + velocity2^2);}
pressure_expr = {pressure = (heat_rat - 1)*(energy - 0.5*density*speed^2);}
temperature_expr = {temperature = pressure/(density*specific_gas_air);}
mach_expr = {mach = speed/sqrt(heat_rat*pressure/density);}
art_visc_expr = {art_visc = art_visc;}
visc_stress_expr = {visc_stress0 = visc_stress0; visc_stress1 = visc_stress1; visc_stress2 = visc_stress2;}
stress_expr = {stress0 = -pressure*normal0 + visc_stress0; stress1 = -pressure*normal1 + visc_stress1; stress2 = -pressure*normal2 + visc_stress2;}
heat_flux_expr = {heat_flux = heat_flux;}
iteration_expr = {iteration = iteration;}
normalized_residual_expr = {normalized_residual = (residual_momentum/init_residual_momentum + residual_density/init_residual_density + residual_energy/init_residual_energy)/3;}
av_advection_residual_expr = {av_advection_residual = av_advection_residual;}
av_diffusion_residual_expr = {av_diffusion_residual = av_diffusion_residual;}
flow_time_expr = {flow_time = flow_time;}
time_step_expr = {time_step = time_step;}
fix_iters_expr = {fix_iters = fix_iters;}
skin_friction_coef_expr = {skin_friction_coef = (visc_stress0^2 + visc_stress1^2 + visc_stress2^2)^.5/(.5*freestream_density*freestream_speed^2);}
pressure_coef_expr = {pressure_coef = (pressure - freestream_pressure)/(.5*freestream_density*freestream_speed^2);}
state_vars = momentum_expr + density_expr + energy_expr + velocity_expr + speed_expr + pressure_expr + temperature_expr + mach_expr
compute_force = {
    integrand_surface = velocity_expr + speed_expr + pressure_expr + stress_expr
    integrate_surface
    force0 = integral_surface_stress0
    force1 = integral_surface_stress1
    force2 = integral_surface_stress2
    drag = force0*freestream_direction0 + force1*freestream_direction1 + force2*freestream_direction2
    lift = 0.
    side_force = 0.
    case1 = {}
    case2 = {lift = force1*freestream_direction0 - force0*freestream_direction1}
    case3 = {
        horiz_norm = (freestream_direction0^2 + freestream_direction1^2)^.5
        lift = force2*horiz_norm - (force0*freestream_direction0 + force1*freestream_direction1)/horiz_norm*freestream_direction2
        side_force = (force1*freestream_direction0 - force0*freestream_direction1)/horiz_norm
    }
    $$({case} + n_dim)
    drag_coef = drag/(0.5*freestream_density*freestream_speed^2)
    lift_coef = lift/(0.5*freestream_density*freestream_speed^2)
    side_force_coef = side_force/(0.5*freestream_density*freestream_speed^2)
}
print_force = {
    $compute_force
    println({drag: } + drag + { N})
    println({lift: } + lift + { N})
    println({side force: } + side_force + { N})
    println({drag coefficient: } + drag_coef)
    println({lift coefficient: } + lift_coef)
    println({side force coefficient: } + side_force_coef)
}
freestream_spec_int_ener = {(freestream3/freestream2 - 0.5*(freestream0^2 + freestream1^2)/freestream2^2)}
freestream_sound_speed_expr = {((heat_rat*(heat_rat - 1)*$freestream_spec_int_ener)^0.5)}
vortex_nondim_veloc = 0.3
vortex_argmax_radius = 0.1
vortex_gaussian = {(vortex_nondim_veloc*exp((1. - ((pos0 - freestream0/freestream2*time)^2 + (pos1 - freestream1/freestream2*time)^2)/vortex_argmax_radius^2)/2.))}
vortex_scalar = {(1 - (heat_rat - 1.)/2*$vortex_gaussian^2)}
vortex_momentum0 = {((freestream0/freestream2 - pos1/vortex_argmax_radius*$vortex_gaussian*$freestream_sound_speed_expr)*$vortex_mass)}
vortex_momentum1 = {((freestream1/freestream2 + pos0/vortex_argmax_radius*$vortex_gaussian*$freestream_sound_speed_expr)*$vortex_mass)}
vortex_mass = {(freestream2*$vortex_scalar^(1./(heat_rat - 1.)))}
vortex_energy = {($freestream_spec_int_ener*$vortex_scalar*$vortex_mass + 0.5*($vortex_momentum0^2 + $vortex_momentum1^2)/$vortex_mass)}
vortex = {
    state0 = $vortex_momentum0
    state1 = $vortex_momentum1
    state2 = $vortex_mass
    state3 = $vortex_energy
}
air_viscosity = {ref_value = 1.716e-5; ref_temperature = 273.; offset = 111.}
air_conductivity = {ref_value = .0241; ref_temperature = 273; offset = 194.}

shock_sub_iters = 10; {number of sub-iterations to run in each nominal iteration to get efficient Chebyshev-accelerated diffusion}
set_shock = {
    art_visc_width = (mesh_extreme01 - mesh_extreme00)*2.^-init_ref_level
    fix_therm_admis = true
    n_cheby_av = shock_sub_iters
    flow_iters = shock_sub_iters
    av_advect_iters = ceil(shock_sub_iters/5.)
}

aerothermo_initial_iters = 1000; {number of initial iterations to run at a small safety factor to let the artificial viscosity catch up}
aerothermo_initial_safety = 3e-3
aerothermo_final_safety = 0.7
set_aerothermo = {
    viscosity_model = air_viscosity
    conductivity_model = air_conductivity
    max_safety = aerothermo_initial_safety
    iterate = iterate + {
        case0 = {}
        case1 = {
            max_safety = aerothermo_final_safety
            println({Initial phase complete. Increasing safety factor to } + max_safety)
        }
        $$({case} + (iteration == aerothermo_initial_iters))
        {alternate between time stepping strategies optimized for convection and diffusion}
        case1 = {
            case0 = {
                flow_iters = 1
                n_cheby_flow = shock_sub_iters
            }
            case1 = {
                flow_iters = shock_sub_iters
                n_cheby_flow = 1
            }
            $$({case} + ((iteration/print_freq)%2 == 0))
        }
        $$({case} + (iteration >= aerothermo_initial_iters))
    }
}

set_geometry_refinement = {
    refine_if = {(uncertainty > geometry_uncertainty | snapping_problem) & ref_level < max_ref_level}
    unrefine_if = {ref_level > init_ref_level & !is_extruded}
}

{scalar parameters}
row_size = 6
init_ref_level = 3
max_ref_level = 10
geometry_uncertainty = 1e-2
viscosity_model = {}
conductivity_model = {}
thermal_bc = {heat_flux = 0.}
surface_bc = {auto}
temperature_offset = 0.
attack = 0.
sideslip = 0.
art_visc_constant = -1.
art_visc_width = -1.
fix_therm_admis = false
working_dir = {hexed_out/}
vis_freq = 10000
iter_width = 10
print_freq = 100
refine_if = {false}
unrefine_if = {false}
max_refinement_iters = 100
n_smooth = 30
geom_n_segments = 1000
vis_surface = true
vis_field = true
vis_tecplot = false
vis_xdmf = true
vis_lts_constraints = false
vis_field_vars = state_vars + art_visc_expr
vis_surface_vars = state_vars + visc_stress_expr + heat_flux_expr + pressure_coef_expr + skin_friction_coef_expr
vis_n_sample = 10
print_vars = iteration_expr + normalized_residual_expr + av_advection_residual_expr + av_diffusion_residual_expr + flow_time_expr + time_step_expr
integrand_field = {}
integrand_surface = {}
max_angle = 10*degree
max_deflection = huge
mesh_extreme_eps = 1e-6
layer_split_points = {split0 = 1./3.;}
init_layer_splits = 0
heat_flux_coercion = 2.

{array parameters}
i_dim = 0
while = {i_dim < 3}
do = {
    mesh_code = {
        mesh_index = {} + i_dim + sign
        $({mesh_extreme} + mesh_index) = 0. + sign
        $({extremal_bc} + mesh_index) = {characteristic}
    }
    sign = 0; $mesh_code
    sign = 1; $mesh_code
    $({flood_fill_start} + i_dim) = huge
    i_dim = i_dim + 1
}
$loop
init_cond = {}
i_var = 0
while = {i_var < 5}
do = {
    init_cond = init_cond + {state} + i_var + { = freestream} + i_var + newline
    i_var = i_var + 1
}
$loop

{overrideable macros}
vis_init = {visualize}
vis_final = {visualize}
print_init = {println header}
print_final = {
    println {simulation complete}
    println({wall clock time = } + wall_time + {s = } + wall_time/minute + {min = } + wall_time/hour + {hr})
    println {}
    println {performance summary:}
    println performance_report
}
done = {println {Error: you forgot to a set stop condition} & throw}
vis_iter = {
    case0 = {}
    case1 = {
        print {visualizing...}
        visualize
        println {done}
    }
    $$({case} + (iteration%vis_freq == 0))
}
print_iter = {println report}
iterate = {
    update
    {compute initial residual for normalization}
    case0 = {}
    case1 = {
        compute_residuals
        init_residual_momentum = residual_momentum
        init_residual_density = residual_density
        init_residual_energy = residual_energy
    }
    $$({case} + (iteration == 1))
    $print_iter
    $vis_iter
    except = {println exception}
    $(read(working_dir + {runtime_cmd.hil}))
    shell({echo > } + working_dir + {runtime_cmd.hil})
}

init = {
    case0 = {}
    case1 = {working_dir = working_dir + {/}}
    $$({case} + (working_dir#(#working_dir - 1) != {/}))
    shell({if [ ! -d } + working_dir + { ]; then mkdir } + working_dir + {; fi})
    case1 = {shell({cp } + input_file + { } + working_dir)}
    $$({case} + (#input_file != 0))
    case1 = {
        case1 = {surface_bc = {nonpenetration}}
        case0 = {surface_bc = {no_slip}}
        $$({case} + (viscosity_model == {}))
    }
    $$({case} + (surface_bc == {auto}))
    case0 = {}
    case1 = {
        i_dim = 0
        while = {i_dim < n_dim}
        do = {
            $({flood_fill_start} + i_dim) = (1 - mesh_extreme_eps)*$({mesh_extreme} + i_dim + 0) + mesh_extreme_eps*$({mesh_extreme} + i_dim + 1)
            i_dim = i_dim + 1
        }
        $loop
    }
    $$({case} + (flood_fill_start0 == huge))
    create_solver
    init_refinement
    add_geom
    i_refinement = 0
    keep_refining = true
    while = {keep_refining & i_refinement < max_refinement_iters}
    do = {
        print({geometry-based refinement sweep } + i_refinement + {... })
        keep_refining = keep_refining & refine
        i_refinement = i_refinement + 1
        println {done}
    }
    $loop
    i_split = 0
    while = {i_split < init_layer_splits}
    do = {split_layers; i_split = i_split + 1}
    $loop
    init_state
    normalized_residual = 1.
    println({initialization complete with } + n_elements + { elements})
    println {}
    $vis_init
}

run = {
    $print_init
    shell({echo > } + working_dir + {runtime_cmd.hil})
    while = {!($done)}
    do = {$iterate}
    $loop
    $print_final
    $vis_final
}
