{simulation of the hypersonic cylinder case of Beckwith & Gallagher: https://apps.dtic.mil/sti/citations/ADA377149}

run_temperature = {
    {mesh setup}
    n_dim = 2
    mesh_extreme00 = -2.
    mesh_extreme01 =  0.
    mesh_extreme10 =  0.
    mesh_extreme11 =  2.
    init_ref_level = 5
    extremal_bc10 = {nonpenetration}
    extremal_bc01 = {pressure_outflow}
    shell {python3 make_geom.py}
    geom0 = {auto.csv}
    surface_max_iters = 0

    {freestream/wall definition}
    mach = 4.15
    stag_temp = (250 - 32)*rankine + zero_celsius
    temperature = stag_temp/(1 + 0.2*mach^2)
    reynolds = 1.28e6
    freestream_visc = 1.713e-5*(temperature/273)^1.5*(273 + 111)/(temperature + 111)
    freestream_cond =   0.0241*(temperature/273)^1.5*(273 + 194)/(temperature + 194)
    diam = 2.
    density = reynolds*freestream_visc/(mach*(1.4*specific_gas_air*temperature)^.5*diam)
    coolant_temp = (coolant_farenheit - 32)*rankine + zero_celsius

    {iteration parameters}
    $set_shock
    done = {iteration >= 6*10^4}
    vis_freq = 10000
    $set_aerothermo
    iterate = iterate + {
        case0 = {}
        case1 = {
            print {splitting layers...}
            split_layers
            println({done. number of elements: } + n_elements)
        }
        $$({case} + (iteration == 2*10^4 | iteration == 3*10^4 | iteration == 4*10^4))
    }

    println {running isothermal case}
    init_layer_splits = 2
    thermal_bc = {conduction = 24.9/(0.115*inch); temperature = } + coolant_temp
    heat_flux_coercion = 0.2
    vis_n_sample = 2
    $init
    println({stagnation temperature: } + stag_temp)
    println({coolant temperature: } + coolant_temp)
    println({freestream thermal conductivity: } + freestream_cond)
    println({freestream dynamic viscosity: } + freestream_visc)
    println({freestream prandtl number: } + freestream_visc*(1.4*specific_gas_air/0.4)/freestream_cond)
    vis_n_sample = 10
    $run
    println {}
}

working_dir = {hexed_out_temperature0}
coolant_farenheit = 60.
$run_temperature

working_dir = {hexed_out_temperature1}
coolant_farenheit = 150.
aerothermo_initial_safety = aerothermo_initial_safety/10
$run_temperature
