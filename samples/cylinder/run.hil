{simulation of the hypersonic cylinder case of Beckwith & Gallagher: https://apps.dtic.mil/sti/citations/ADA377149}

{mesh setup}
n_dim = 2
mesh_extreme00 = -2.
mesh_extreme01 =  0.
mesh_extreme10 =  0.
mesh_extreme11 =  2.
init_ref_level = 5
extremal_bc10 = {nonpenetration}
extremal_bc01 = {pressure_outflow}
shell {python3 make_geom.py}
geom0 = {auto.csv}
surface_max_iters = 0

{freestream/wall definition}
mach = 4.15
stag_temp = (250 - 32)*rankine + zero_celsius
temperature = stag_temp/(1 + 0.2*mach^2)
reynolds = 1.28e6
freestream_visc = 1.713e-5*(temperature/273)^1.5*(273 + 111)/(temperature + 111)
freestream_cond =   0.0241*(temperature/273)^1.5*(273 + 194)/(temperature + 194)
diam = 2.
density = reynolds*freestream_visc/(mach*(1.4*specific_gas_air*temperature)^.5*diam)
wall_temp = 0.8*stag_temp
println({stagnation temperature: } + stag_temp)
println({wall temperature: } + wall_temp)
println({freestream thermal conductivity: } + freestream_cond)
println({freestream dynamic viscosity: } + freestream_visc)
println({freestream prandtl number: } + freestream_visc*(1.4*specific_gas_air/0.4)/freestream_cond)

{iteration parameters}
$set_shock
done = {iteration >= 6*10^4}
vis_freq = 10000
$set_aerothermo
iterate = iterate + {
    case0 = {}
    case1 = {
        print {splitting layers...}
        split_layers
        println({done. number of elements: } + n_elements)
    }
    $$({case} + (iteration == 2*10^4 | iteration == 3*10^4 | iteration == 4*10^4))
}

println {running isothermal case}
init_layer_splits = 2
thermal_bc = {temperature = } + wall_temp
working_dir = {hexed_out_isothermal1}
vis_n_sample = 2
$init
vis_n_sample = 10
$run
println {}

println {running adiabatic case}
init_layer_splits = 1
thermal_bc = {heat_flux = 0.}
working_dir = {hexed_out_adiabatic1}
vis_n_sample = 2
$init
vis_n_sample = 10
$run
println {}
